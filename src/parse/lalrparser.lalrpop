use crate::diagnostic::Diagnostic;
use crate::parse::lexer::{Token};
use crate::meta::{self, Meta};
use crate::ident::{Ident, ResIdent};
use crate::pos::{Sp, Span};
use crate::ast;
use super::lalrparser_util as util;

grammar<'input, 'st>(state: &'st mut crate::parse::State);

extern {
    type Location = crate::parse::lexer::Location;
    type Error = Diagnostic;

    enum Token<'input> {
        // Terminals
        STRING => Token::LitString(<&'input str>),
        FLOAT => Token::LitFloat(<&'input str>),
        FLOAT_RAD => Token::LitRad(<&'input str>),
        INT => Token::LitIntDec(<&'input str>),
        INT_HEX => Token::LitIntHex(<&'input str>),
        INT_BIN => Token::LitIntBin(<&'input str>),
        DIFFICULTY => Token::Difficulty(<&'input str>),
        INSTR => Token::Instr(<&'input str>),
        IDENT => Token::Ident(<&'input str>),
        "," => Token::Comma,
        "?" => Token::Question,
        ":" => Token::Colon,
        ";" => Token::Semicolon,
        "[" => Token::BracketOpen,
        "]" => Token::BracketClose,
        "{" => Token::BraceOpen,
        "}" => Token::BraceClose,
        "(" => Token::ParenOpen,
        ")" => Token::ParenClose,
        "@" => Token::AtSign,
        "..." => Token::Ellipsis,
        "." => Token::Dot,
        "=" => Token::Eq,
        "+" => Token::Plus,
        "-" => Token::Minus,
        "*" => Token::Star,
        "/" => Token::Slash,
        "%" => Token::Percent,
        "^" => Token::Caret,
        "|" => Token::Or,
        "&" => Token::And,
        "+=" => Token::PlusEq,
        "-=" => Token::MinusEq,
        "*=" => Token::StarEq,
        "/=" => Token::SlashEq,
        "%=" => Token::PercentEq,
        "^=" => Token::CaretEq,
        "|=" => Token::OrEq,
        "&=" => Token::AndEq,
        "==" => Token::EqEq,
        "!=" => Token::NotEq,
        "<" => Token::Less,
        "<=" => Token::LessEq,
        ">" => Token::Greater,
        ">=" => Token::GreaterEq,
        "!" => Token::Not,
        "||" => Token::OrOr,
        "&&" => Token::AndAnd,
        "--" => Token::MinusMinus,
        "$" => Token::Cash,
        "#" => Token::Hash,
        "anim" => Token::Anim,
        "ecli" => Token::Ecli,
        "meta" => Token::Meta,
        "sub" => Token::Sub,
        "timeline" => Token::Timeline,
        "script" => Token::Script,
        "entry" => Token::Entry,
        "var" => Token::Var,
        "int" => Token::Int,
        "float" => Token::Float,
        "string" => Token::String,
        "void" => Token::Void,
        "const" => Token::Const,
        "inline" => Token::Inline,
        "insdef" => Token::Insdef,
        "return" => Token::Return,
        "goto" => Token::Goto,
        "loop" => Token::Loop,
        "if" => Token::If,
        "else" => Token::Else,
        "unless" => Token::Unless,
        "do" => Token::Do,
        "while" => Token::While,
        "times" => Token::Times,
        "break" => Token::Break,
        "switch" => Token::Switch,
        "case" => Token::Case,
        "default" => Token::Default,
        "interrupt" => Token::Interrupt,
        "async" => Token::Async,
        "global" => Token::Global,
        "false" => Token::False,
        "true" => Token::True,
        "pragma" => Token::Pragma,
        "mapfile" => Token::Mapfile,
        "image_source" => Token::ImageSource,
        "sin" => Token::Sin,
        "cos" => Token::Cos,
        "sqrt" => Token::Sqrt,
        "_S" => Token::CastI,
        "_f" => Token::CastF,
        "REG" => Token::Reg,
        // Virtual tokens for parsable nonterminals
        Dispatch_ScriptFile => Token::VirtualDispatch(crate::parse::AnythingTag::ScriptFile),
        Dispatch_Item => Token::VirtualDispatch(crate::parse::AnythingTag::Item),
        Dispatch_Block => Token::VirtualDispatch(crate::parse::AnythingTag::Block),
        Dispatch_Stmt => Token::VirtualDispatch(crate::parse::AnythingTag::Stmt),
        Dispatch_Expr => Token::VirtualDispatch(crate::parse::AnythingTag::Expr),
        Dispatch_Var => Token::VirtualDispatch(crate::parse::AnythingTag::Var),
        Dispatch_LitString => Token::VirtualDispatch(crate::parse::AnythingTag::LitString),
        Dispatch_Meta => Token::VirtualDispatch(crate::parse::AnythingTag::Meta),
    }
}

// =================================================
// Parser dispatch

/// The parser's "designated driver."
///
/// Each nonterminal marked 'pub' in a lalrpop file generates an independent parser,
/// and the code sharing between them is inefficient. Thus, one can reduce compile
/// times a pretty fair deal by only having one public parser, which dispatches
/// to other nonterminals based on a virtual token inserted by the caller at
/// the beginning of the token stream.
///
/// Don't use this directly; use the [`crate::parse::Parse`] trait, which takes
/// care of all the silly virtual dispatch business.
pub(crate) Anything: Sp<crate::parse::AnythingValue> = Sp<AnythingInner>;

AnythingInner: crate::parse::AnythingValue = {
    Dispatch_ScriptFile <x:ScriptFile> => crate::parse::AnythingValue::ScriptFile(x),
    Dispatch_Item <x:Item> => crate::parse::AnythingValue::Item(x),
    Dispatch_Block <x:Block> => crate::parse::AnythingValue::Block(x),
    Dispatch_Stmt <x:Stmt> => crate::parse::AnythingValue::Stmt(x),
    Dispatch_Expr <x:Expr> => crate::parse::AnythingValue::Expr(x),
    Dispatch_Var <x:Var> => crate::parse::AnythingValue::Var(x),
    Dispatch_LitString <x:LitString> => crate::parse::AnythingValue::LitString(x),
    Dispatch_Meta <x:Meta> => crate::parse::AnythingValue::Meta(x),
};

// =================================================
// Items

ScriptFile: ast::ScriptFile = {
    <items:Items> => {
        let mapfiles = state.mapfiles.clone();
        let image_sources = state.image_sources.clone();
        ast::ScriptFile { items, mapfiles, image_sources }
    }
};

Items: Vec<Sp<ast::Item>> = {
    => vec![],

    <items:Items> <item:Sp<Item>> => util::push(items, item),

    // FIXME: this syntax likely won't stay.  People might be confused that it is treated
    //        in a declarative manner, and I don't know what I want our preprocessing solution
    //        to look like
    <items:Items> "#" "pragma" "mapfile" <file:Sp<LitString>> => {
        state.mapfiles.push(file);
        items
    },
    <items:Items> "#" "pragma" "image_source" <file:Sp<LitString>> => {
        state.image_sources.push(file);
        items
    },
};

Item: ast::Item = {
    ItemFunc,
    ItemAnmScript,
    ItemMeta,
    ItemConstVar,
    ItemTimeline,
};

ItemAnmScript: ast::Item = {
    <keyword:TokenSpan<"script">> <number:Sp<LitIntSigned>?> <ident:Sp<Ident>>
        <code:FuncRootBlock>
        => ast::Item::AnmScript { keyword, number, ident, code },
};

ItemTimeline: ast::Item = {
    <keyword:TokenSpan<"timeline">> <number:Sp<LitIntSigned>> <code:FuncRootBlock>
        => ast::Item::Timeline { keyword, number, code },
};

ItemMeta: ast::Item = {
    <keyword:Sp<MetaKeyword>> <fields:Sp<MetaBracedFields>>
        => ast::Item::Meta { keyword, fields },
};

ItemConstVar: ast::Item = {
    "const" <ty_keyword:Sp<ConstVarTypeKeyword>> <vars:SeparatedStrict<Sp<ConstDeclarationListItem>, ",">> ";"
        => ast::Item::ConstVar { ty_keyword, vars },
};

ItemFunc: ast::Item = {
    <item:ItemFuncHeader> ";" => item,

    <item:ItemFuncHeader> <code:FuncRootBlock> => {
        match item {
            ast::Item::Func { qualifier, ty_keyword, ident, params, code: _ }
                => ast::Item::Func { qualifier, ty_keyword, ident, params, code: Some(code) },
            _ => unreachable!(),
        }
    },
};

ItemFuncHeader: ast::Item = {
    <qualifier:Sp<SingleFuncQualifier>?> <ty_keyword:Sp<FuncTypeKeyword>> <ident:Sp<ResIdent>>
        "(" <params:SeparatedTrailing<FuncParam, ",">> ")"
        => ast::Item::Func { qualifier, ty_keyword, ident, params, code: None },

    Sp<SingleFuncQualifier> <second:Sp<SingleFuncQualifier>> ! =>? Err(error!(
        message("too many function qualifiers"),
        primary(second, "extra qualifier"),
    ).into()),
};

#[inline]
SingleFuncQualifier: ast::FuncQualifier = {
    "const" => ast::FuncQualifier::Const,
    "inline" => ast::FuncQualifier::Inline,
};

FuncParam: (Sp<ast::TypeKeyword>, Sp<ResIdent>) = {
    <Sp<LocalVarTypeKeyword>> <Sp<ResIdent>>,
};

FuncBodyOrSemicolon: Option<ast::Block> = {
    ";" => None,
    <code:Block> => Some(code),
};

MetaKeyword: ast::MetaKeyword = {
    "entry" => ast::MetaKeyword::Entry,
    "meta" => ast::MetaKeyword::Meta,
};

// NOTE: Working with state in LALRPOP is tricky as there are no midrules like bison.
//
// When working with state, you should generally prefer these non-inline versions, as the actions
// will intuitively take place at the location they appear in the grammar.
_TimeStackEnter: () = _TimeStackEnterInline;
_TimeStackPop: () = _TimeStackPopInline;
_TimeGet: i32 = _TimeGetInline;

// Sometimes, unfortunately, the above rules lead to parsing ambiguities and you may need to
// resort to these inline versions.
//
// The inline versions are only executed after the whole rule has been parsed. Thus, you should only
// ever use them at the END OF A RULE; this may require breaking a rule in half.
#[inline] _TimeStackEnterInline: () = => { state.time_stack.push(0); };
#[inline] _TimeStackPopInline: () = => { state.time_stack.pop(); };
#[inline] _TimeGetInline: i32 = => *state.time_stack.last().unwrap();

// ==================================================
// Metadata in a JSON-ish format, used by ANM

Meta: Meta = {
    <x:Sp<Expr>> => Meta::Scalar(x),
    <x:Sp<MetaBracedFields>> => Meta::Object(x),
    <name:Sp<Ident>> ":"? <fields:Sp<MetaBracedFields>> => Meta::Variant { name, fields },
    "[" <items:SeparatedTrailing<Sp<Meta>, ",">> "]" => Meta::Array(items),
};

MetaBracedFields: meta::Fields = {
    "{" <kvs:SeparatedTrailing<(<Sp<PossiblyNumericIdent>> ":" <Sp<Meta>>), ",">> "}" =>? {
        let mut map = indexmap::IndexMap::with_capacity(kvs.len());
        for (key, value) in kvs {
            let new_key_span = key.span;
            match map.entry(key) {
                indexmap::map::Entry::Vacant(entry) => { entry.insert(value); },
                indexmap::map::Entry::Occupied(entry) => return Err(error!(
                    message("duplicate metadata field '{}'", entry.key()),
                    primary(new_key_span, "duplicate key"),
                    secondary(entry.key().span, "previously supplied here"),
                ).into()),
            }
        }
        Ok(map)
    },
};

#[inline]
PossiblyNumericIdent: Ident = {
    <x:LitIntUnsigned> => x.to_string().parse().expect("cannot fail"),
    <x:Ident> => x,
};

// ==================================================
// Statements

Stmt: ast::Stmt = {
    <stuff:StmtLabelsWithTime> <body:StmtBody>
        => ast::Stmt { time: stuff.1, body },
};

StmtBody: ast::StmtBody = {
    <item:Box<Sp<ItemConstVar>>> => ast::StmtBody::Item(item),

    <item:Box<Sp<ItemFunc>>> => ast::StmtBody::Item(item),

    <keyword:TokenSpan<"return">> <value:Sp<Expr>?> ";"
        => ast::StmtBody::Return { keyword, value },

    <chain:CondChain>
        => ast::StmtBody::CondChain(chain),

    <keyword:Sp<CondKeyword>> "(" <cond:Sp<Cond>> ")" <goto:StmtGoto> ";"
        => ast::StmtBody::CondGoto { keyword, cond, goto },

    <do_keyword:TokenSpan<"do">> <block:StmtOrBlock> <while_keyword:TokenSpan<"while">> "(" <cond:Sp<Cond>> ")" ";"
        => ast::StmtBody::While { do_keyword: Some(do_keyword), while_keyword, cond, block },

    <while_keyword:TokenSpan<"while">> "(" <cond:Sp<Cond>> ")" <block:StmtOrBlock>
        => ast::StmtBody::While { do_keyword: None, while_keyword, cond, block },

    <keyword:TokenSpan<"times">> "(" <clobber:(<Sp<Var>> "=")?> <count:Sp<Expr>> ")" <block:StmtOrBlock>
        => ast::StmtBody::Times { keyword, clobber, count, block },

    <keyword:TokenSpan<"loop">> <block:StmtOrBlock>
        => ast::StmtBody::Loop { keyword, block },

    <goto:StmtGoto> ";"
        => ast::StmtBody::Goto(goto),

    <e:Sp<Expr>> ";"
        => ast::StmtBody::Expr(e),

    <name:Sp<Ident>> ":"
        => ast::StmtBody::Label(name),

    "interrupt" "[" <arg:Sp<LitIntSigned>> "]" ":"
        => ast::StmtBody::InterruptLabel(arg),

    <var:Sp<Var>> <op:Sp<OpAssign>> <value:Sp<Expr>> ";"
        => ast::StmtBody::Assignment { var, op, value },

    <ty_keyword:Sp<LocalVarTypeKeyword>> <vars:SeparatedStrict<Sp<StmtDeclarationListItem>, ",">> ";"
        => ast::StmtBody::Declaration { ty_keyword, vars },

    StmtSpecialCall,
}

StmtTimeLabel: () = {
    "+" <dt:LitIntUnsigned> ":" => { *state.time_stack.last_mut().expect("empty time stack?! (bug)") += dt; },
    <t:LitIntSigned> ":" => { *state.time_stack.last_mut().expect("empty time stack?! (bug)") = t; },
};

/// Read all statement labels and then get the time at the end of them.
///
/// This exists because it is obscenely difficult to make unambiguous while getting the time read correctly.
/// (though in retrospect, it does kind of make sense that the parser would have to speculatively check the
///  time after every label it parses)
StmtLabelsWithTime: (Vec<Sp<ast::StmtLabel>>, i32) = {
    <time: _TimeGetInline> => (vec![], time),
    <out:StmtLabels> StmtTimeLabel <time: _TimeGetInline> => (out, time),
    <out:StmtLabels> <label:Sp<StmtLabel>> <time: _TimeGetInline> => (util::push(out, label), time),
};

#[inline]
StmtLabels: Vec<Sp<ast::StmtLabel>> = <out:StmtLabelsWithTime> => out.0;

StmtLabel: ast::StmtLabel = {
    <flags:DifficultyLabel> => ast::StmtLabel::Difficulty { flags, temporary: false },
    <flags:DifficultyLabel> ":" => ast::StmtLabel::Difficulty { flags, temporary: true },
};

StmtGoto: ast::StmtGoto = {
    "goto" <destination:Sp<Ident>> <time:("@" <Sp<LitIntSigned>>)?>
        => ast::StmtGoto { destination, time },
};

StmtSpecialCall: ast::StmtBody = {
    // handle cases where at least one of the '@' or 'async' are present
    "@" <func:Sp<Ident>> <args:ExprCallParenArgs> <async_:CallAsyncKind?> ";" => {
        ast::StmtBody::CallSub { at_symbol: true, func, args, async_ }
    },
    <func:Sp<Ident>> <args:ExprCallParenArgs> <async_:CallAsyncKind> ";" => {
        ast::StmtBody::CallSub { at_symbol: true, func, args, async_: Some(async_) }
    },
};

CallAsyncKind: ast::CallAsyncKind = {
    "async" => ast::CallAsyncKind::CallAsync,
    "async" <id:Box<Sp<Expr>>> => ast::CallAsyncKind::CallAsyncId(id),
};

StmtDeclarationListItem: (Sp<ast::Var>, Option<Sp<ast::Expr>>) = {
    <var:Sp<VarIdent>> => (var, None),
    <var:Sp<VarIdent>> "=" <value:Sp<Expr>> => (var, Some(value)),
};

ConstDeclarationListItem: (Sp<ast::Var>, Sp<ast::Expr>) = {
    <tuple:StmtDeclarationListItem> =>? match tuple {
        (var, Some(expr)) => Ok((var, expr)),
        (var, None) => Err(error!(
            message("uninitialized const var"),
            primary(var, "needs a value"),
        ).into()),
    },
};

CondChain: ast::StmtCondChain = {
    <chain:CondChainWithoutElse> => chain,
    <mut chain:CondChainWithoutElse> "else" <else_block:StmtOrBlock> => {
        assert!(chain.else_block.is_none());
        chain.else_block = Some(else_block);
        chain
    },
};
CondChainWithoutElse: ast::StmtCondChain = {
    <cond_block:CondBlock> => {
        ast::StmtCondChain { cond_blocks: vec![cond_block], else_block: None }
    },
    <mut chain:CondChainWithoutElse> "else" <cond_block:CondBlock> => {
        chain.cond_blocks.push(cond_block);
        chain
    },
};

CondBlock: ast::CondBlock = {
    <keyword:Sp<CondKeyword>> "(" <cond:Sp<Cond>> ")" <block:StmtOrBlock>
        => ast::CondBlock { keyword, cond, block },
};

Cond: ast::Cond = {
    "--" <var:Sp<Var>> => ast::Cond::PreDecrement(var),
    <expr:Sp<Expr>> => ast::Cond::Expr(expr),
};

// block for an 'if', 'while', etc.
StmtOrBlock: ast::Block = {
    // FIXME: Todo: single statement.  (creates ambiguous grammar, how to fix?)
//    <st:Stmt> => vec![st],
    Block,
};

CondKeyword: ast::CondKeyword = {
    "if" => token![if],
    "unless" => token![unless],
};

DifficultyLabel: ast::DifficultyLabel = {
    // TODO: Actually parse
    <l: @L> <s:DIFFICULTY> <r: @R> =>? Err(error!(
        message("unsupported syntax"),
        primary(Span::from_locs(l, r), "difficulty flags not yet implemented"),
    ).into()),
};

Block: ast::Block = "{" <BlockInnards> "}";

#[inline]
BlockInnards: ast::Block = {
    <start_span:Span> <start_time:_TimeGet> <mut code:Sp<Stmt>*>
        <end_labels:Sp<StmtLabels>> <end_time:_TimeGet> <end_span:Span>
        => {
        // bookend with empty statements to simplify time and jump logic
        code.insert(0, sp!(start_span => ast::Stmt {
            time: start_time,
            body: ast::StmtBody::NoInstruction,
        }));
        code.push(sp!(end_labels.span => ast::Stmt {
            time: end_time,
            body: ast::StmtBody::NoInstruction,
        }));
        ast::Block(code)
    },
};

/// Same as a block but it pushes a new time context. (so time starts at 0 inside and resets outside)
//
// We have to split this rule up very carefully to make sure that all of the changes to mutable
// state happen at exactly the right times.  This is the price we must pay for mutable state.
FuncRootBlock_1: () = "{" _TimeStackEnterInline;
FuncRootBlock_2: ast::Block = FuncRootBlock_1 <BlockInnards>;
FuncRootBlock: ast::Block =
    // Inline pop is important. The non-inline version will scan to the next token and make the span too large.
    // (see https://github.com/ExpHP/truth/issues/18)
    <FuncRootBlock_2> "}" _TimeStackPopInline;

// ==================================================
// Expressions

// Precedence is encoded in the use of these macros
RightTernary<NextTier>: ast::Expr = {
    <cond:Box<Sp<NextTier>>>
        <question:TokenSpan<"?">> <left:Box<Sp<NextTier>>>
        <colon:TokenSpan<":">> <right:Box<Sp<RightTernary<NextTier>>>>
        => ast::Expr::Ternary { cond, left, right, question, colon },
    NextTier,
};
LeftBinop<Op, NextTier>: ast::Expr = {
    <a:Box<Sp<LeftBinop<Op, NextTier>>>>
        <op:Sp<Op>>
        <b:Box<Sp<NextTier>>>
        => ast::Expr::Binop(a, op, b),
    NextTier,
};
LeftUnop<Op, NextTier>: ast::Expr = {
    // no recursion; only allow one unary op
    <op:Sp<Op>> <e:Box<Sp<NextTier>>> => ast::Expr::Unop(op, e),
    NextTier,
};

Expr: ast::Expr = ExprWithOpers;

// Operator precedence table
ExprWithOpers = ExprTernary;
ExprTernary = RightTernary<ExprBinopOr>;
ExprBinopOr = LeftBinop<OpOr, ExprBinopAnd>;
ExprBinopAnd = LeftBinop<OpAnd, ExprBinopBitOr>;
ExprBinopBitOr = LeftBinop<OpBitOr, ExprBinopBitXor>;
ExprBinopBitXor = LeftBinop<OpBitXor, ExprBinopBitAnd>;
ExprBinopBitAnd = LeftBinop<OpBitAnd, ExprBinopCmpEq>;
ExprBinopCmpEq = LeftBinop<OpCmpEq, ExprBinopCmpOrd>;
ExprBinopCmpOrd = LeftBinop<OpCmpOrd, ExprBinopAddLike>;
ExprBinopAddLike = LeftBinop<OpAddLike, ExprBinopMulLike>;
ExprBinopMulLike = LeftBinop<OpMulLike, ExprUnop>;
ExprUnop = LeftUnop<OpLeftUnary, ExprTerm>;

OpOr: ast::BinopKind = "||" => token![||];
OpAnd: ast::BinopKind = "&&" => token![&&];
OpBitOr: ast::BinopKind = "|" => token![|];
OpBitXor: ast::BinopKind = "^" => token![^];
OpBitAnd: ast::BinopKind = "&" => token![&];
OpCmpEq: ast::BinopKind = {
    "==" => token![==],
    "!=" => token![!=],
};
OpCmpOrd: ast::BinopKind = {
    ">" => token![>],
    "<" => token![<],
    ">=" => token![>=],
    "<=" => token![<=],
};
OpAddLike: ast::BinopKind = {
    "+" => token![+],
    "-" => token![-],
};
OpMulLike: ast::BinopKind = {
    "*" => token![*],
    "/" => token![/],
    "%" => token![%],
};
OpAssign: ast::AssignOpKind = {
    "=" => token![=],
    "+=" => token![+=],
    "-=" => token![-=],
    "*=" => token![*=],
    "/=" => token![/=],
    "%=" => token![%=],
    "|=" => token![|=],
    "^=" => token![^=],
    "&=" => token![&=],
};
#[inline] OpLeftUnary: ast::UnopKind = {
    "-" => token![-],
    "!" => token![!],
};

FuncUnopKeyword: ast::UnopKind = {
    "sin" => token![sin],
    "cos" => token![cos],
    "sqrt" => token![sqrt],
    "_S" => token![_S],
    "_f" => token![_f],
};

// Expressions that don't contain a toplevel operator
ExprTerm: ast::Expr = {
    "(" <e:Expr> ")" => e,

    <op:Sp<FuncUnopKeyword>> "(" <e:Box<Sp<Expr>>> ")"
        => ast::Expr::Unop(op, e),

    <name:Sp<CallableName>> <args:ExprCallParenArgsWithPseudos>
        => ast::Expr::Call { name, pseudos: args.0, args: args.1 },

    <v:Sp<Var>> => ast::Expr::Var(v),

    <value:LitIntUnsigned> => ast::Expr::LitInt { value, radix: ast::IntRadix::Dec },

    <value:LitFloatUnsigned> => ast::Expr::LitFloat { value },

    <x:LitString> => ast::Expr::LitString(x),

    // This syntax is reserved.
    // The only reason we parse it right now is just to prove that adding it wouldn't make the grammar ambiguous.
    <l: @L> <_v: Var> "[" <_e: Expr> "]" <r: @R> =>? Err(error!(
        message("invalid operation"),
        primary(Span::from_locs(l, r), "array indexing is not a thing, sorry"),
    ).into()),
};

#[inline]
CallableName: ast::CallableName = {
    <ident:ResIdent> => ast::CallableName::Normal { ident, language_if_ins: None },
    <opcode:RawInsIdent> => ast::CallableName::Ins { opcode, language: None },
};

ExprCallParenArgs: Vec<Sp<ast::Expr>> = {
    <tup:ExprCallParenArgsWithPseudos> =>? {
        let (pseudos, args) = tup;
        match pseudos.len() {
            0 => Err(error!(
                message("unexpected pseudo-arg"),
                primary(pseudos[0].tag_span(), "only permitted in instruction calls"),
            ).into()),
            _ => Ok(args),
        }
    }
};

ExprCallParenArgsWithPseudos: (Vec<Sp<ast::PseudoArg>>, Vec<Sp<ast::Expr>>) = {
    "(" <mut things:SeparatedTrailing<Either<Sp<PseudoArg>, Sp<Expr>>, ",">> ")" =>? {
        let pseudo_end = things.iter().position(|x| matches!(x, util::Either::That(_))).unwrap_or_else(|| things.len());

        let args = things.drain(pseudo_end..).map(|x| match x {
            util::Either::That(arg) => Ok(arg),
            util::Either::This(pseudo) => Err(error!(
                message("invalid pseudo-arg"),
                primary(pseudo.tag_span(), "must come before other arguments"),
            )),
        }).collect::<Result<_, _>>()?;

        let pseudos = things.into_iter().map(|x| match x {
            util::Either::This(x) => x,
            util::Either::That(_) => unreachable!(),
        }).collect();

        Ok((pseudos, args))
    },
};

PseudoArg: ast::PseudoArg = {
    <at_sign:TokenSpan<"@">> <kind:Sp<PseudoArgKind>> <eq_sign:TokenSpan<"=">> <value:Sp<Expr>>
        => ast::PseudoArg { at_sign, kind, eq_sign, value }
};

PseudoArgKind: ast::PseudoArgKind = {
    <l: @L> <ident:Ident> <r: @R> =>? {
        const PAIRS: &'static [(&'static str, ast::PseudoArgKind)] = &[
            ("pop", token![pop]),
            ("mask", token![mask]),
            ("blob", token![blob]),
        ];

        PAIRS.iter()
            .filter(|&&(str, _)| ident == *str)
            .map(|&(_, token)| token)
            .next().ok_or_else(|| {
                let expected = PAIRS.iter().map(|x| x.0).collect::<Vec<_>>().join(", ");
                error!(
                    message("invalid pseudo-arg"),
                    primary(Span::from_locs(l, r), "not a pseudo-arg"),
                    note("valid pseudo-args are: {}", expected),
                ).into()
            })
    },
};

// ==================================================

LitString: ast::LitString = {
    <l: @L> <s:STRING> <r: @R> =>? {
        let mut out = String::new();
        let mut escape = false;
        for c in s[1..s.len()-1].chars() {
            if escape {
                escape = false;
                match c {
                    '0' => out.push_str("\0"),
                    '"' => out.push_str("\""),
                    '\\' => out.push_str("\\"),
                    'n' => out.push_str("\n"),
                    'r' => out.push_str("\r"),
                    _ => {
                        let escape = match c.is_ascii_graphic() {
                            true => format!("'{}'", c),
                            false => format!("U+{:04x}", c as u32),
                        };
                        return Err(error!(
                            message("invalid escape character {}", escape),
                            primary(Span::from_locs(l, r), "contains invalid escape"),
                        ).into());
                    },
                }
            } else if c == '\\' {
                escape = true;
            } else {
                out.push(c);
            }
        }
        assert!(!escape);  // should be impossible due to the token's regex
        Ok(ast::LitString { string: out })
    },
};

LitFloatSigned: f32 = {
    "-" <x:LitFloatUnsigned> => -x,
    <x:LitFloatUnsigned> => x,
};

LitIntSigned: i32 = {
    "-" <x:LitIntUnsigned> => i32::wrapping_neg(x),
    <x:LitIntUnsigned> => x,
};

LitFloatUnsigned: f32 = {
    <mut s:FLOAT> => {
        if s.ends_with("f") || s.ends_with("F") {
            s = &s[..s.len()-1];
        }
        util::parse_ascii(s).unwrap()
    },
    <mut s:FLOAT_RAD> => {
        s = &s["rad(".len()..];
        s = &s[..s.len()-")".len()];
        if s.ends_with("f") || s.ends_with("F") {
            s = &s[..s.len()-1];
        }
        util::parse_ascii::<_, f32>(s).unwrap().to_radians()
    },
};

LitIntUnsigned: i32 = {
    <l: @L> <result: LitIntUnsigned_Result> <r: @R> =>? {
        // Note: Parsing numbers in the range 2^31..2^32 without overflow errors is deliberately supported
        result.map(|x: u32| x as i32)
            .map_err(|e| error!(
                message("bad integer literal"),
                primary(Span::from_locs(l, r), "{}", e)
            ).into())
    },
};

LitIntUnsigned_Result: Result<u32, std::num::ParseIntError> = {
    <l: @L> <s:INT> <r: @R> => {
        #![allow(unused_braces)]  // without these braces, lalrpop can't parse this turbofish
        util::parse_ascii::<_, u32>(s)
    },
    <l: @L> <s:INT_HEX> <r: @R> => util::u32_from_ascii_radix(&s[2..], 16),
    <l: @L> <s:INT_BIN> <r: @R> => util::u32_from_ascii_radix(&s[2..], 2),
    "true" => Ok(1),
    "false" => Ok(0),
};

// ==================================================
// Variables

Var: ast::Var = {
    <ty_sigil:VarSigil> <name:VarName> => ast::Var { ty_sigil, name },
};

#[inline]
VarSigil: Option<ast::VarSigil> = {
    => None,
    "$" => Some(token![sigil $]),
    "%" => Some(token![sigil %]),
};

VarName: ast::VarName = {
    <ident:ResIdent>
        => ast::VarName::Normal { ident, language_if_reg: None },

    "REG" "[" <sign:OptionalMinus> <x:LitIntUnsigned> "]"
        => ast::VarName::Reg { reg: i32::wrapping_mul(x, sign).into(), language: None },
};

VarIdent: ast::Var = <ident:ResIdent>
    => ast::Var { ty_sigil: None, name: ast::VarName::Normal { ident, language_if_reg: None } };

OptionalMinus: i32 = {
    "-" => -1,
    => 1,
};

#[inline]
ResIdent: ResIdent = <ident:Ident> => ResIdent::new_null(ident);

Ident: Ident = {
    <l: @L> <tok:IdentStr> <r: @R> => {
        tok.parse().expect("cannot fail; ASCII, and ins_ is parsed elsewhere")
    },
};

IdentStr: &'input str = {
    // obviously
    IDENT,
    // contextual keywords
    <s: "mapfile"> => s.as_str(),
    <s: "entry"> => s.as_str(),
    <s: "anim"> => s.as_str(),
    <s: "ecli"> => s.as_str(),
    <s: "timeline"> => s.as_str(),
    <s: "script"> => s.as_str(),
    <s: "default"> => s.as_str(),
    <s: "case"> => s.as_str(),
};

RawInsIdent: u16 = {
    <l:@L> <ins_ident:INSTR> <r:@R> =>? {
        use regex::Regex;

        lazy_static::lazy_static!{
            static ref CANONICAL_INT_RE: Regex = Regex::new("^(0|[1-9][0-9]*)$").unwrap();
        }

        assert!(ins_ident.starts_with("ins_"));
        let num_str = &ins_ident["ins_".len()..];
        if !CANONICAL_INT_RE.is_match(num_str) {
            return Err(error!(
                message("invalid instruction identifier"),
                primary(Span::from_locs(l, r), "invalid instruction identifier"),
                note(r#"identifiers beginning with 'ins_' must end in a canonically-formatted integer, e.g. 'ins_42'."#),
            ).into());
        }

        num_str.parse()
            // this error is mostly for overflow
            .map_err(|e| error!(
                message("invalid instruction identifier"),
                primary(Span::from_locs(l, r), "{}", e),
            ).into())
    }
};

// ==================================================
// Type

#[inline]
FuncTypeKeyword: ast::TypeKeyword = {
    <keyword:Sp<TypeKeyword>> =>? match keyword.value {
        token![var] => Err(error!(
            message("var-typed function"),
            primary(keyword, "cannot be used on functions"),
        ).into()),
        kw => Ok(kw),
    }
};

#[inline]
ConstVarTypeKeyword: ast::TypeKeyword = {
    <keyword:Sp<VarTypeKeyword>> =>? match keyword.value {
        token![var] => return Err(error!(
            message("illegal untyped const"),
            primary(keyword, "const vars must have a type"),
        ).into()),
        kw => Ok(kw),
    },
};

#[inline]
LocalVarTypeKeyword: ast::TypeKeyword = {
    <keyword:Sp<VarTypeKeyword>> =>? match keyword.value {
        token![string] => Err(error!(
            message("non-const string variable"),
            primary(keyword, "only possible for const vars"),
        ).into()),
        kw => Ok(kw),
    },
};

#[inline]
VarTypeKeyword: ast::TypeKeyword = {
    <keyword:Sp<TypeKeyword>> =>? match keyword.value {
        token![void] => Err(error!(
            message("void-typed variable"),
            primary(keyword, "variables must have a value"),
        ).into()),
        kw => Ok(kw),
    },
};

TypeKeyword: ast::TypeKeyword = {
    "int" => token![int],
    "float" => token![float],
    "string" => token![string],
    "var" => token![var],
    "void" => token![void],
};

// ==================================================
// Utility

/// Parse something while obtaining its span.
#[inline]
Sp<Rule>: Sp<Rule> = {
    <l: @L> <rule: Rule> <r: @R> => Sp {
        span: Span::from_locs(l, r),
        value: rule,
    },
}
/// Get a zero-length span at the current position.
Span: Span = <sp:Sp<()>> => sp.span;

TokenSpan<Rule>: Sp<()> = <r:Sp<Rule>> => sp!(r.span => ());

Box<Rule>: Box<Rule> = <r:Rule> => Box::new(r);

Either<A, B>: util::Either<A, B> = {
    <a: A> => util::Either::This(a),
    <b: B> => util::Either::That(b),
};

/// Parse a separated sequence with an optional trailing separator.
SeparatedTrailing<T, Sep>: Vec<T> = {
    => vec![],
    <SeparatedStrictNonempty<T, Sep>>,
    <SeparatedStrictNonempty<T, Sep>> Sep,
};

/// Parse a separated sequence with no trailing separator.
SeparatedStrict<T, Sep>: Vec<T> = {
    => vec![],
    <SeparatedStrictNonempty<T, Sep>>,
};
SeparatedStrictNonempty<T, Sep>: Vec<T> = {
    <x:T> => vec![x],
    <xs:SeparatedStrictNonempty<T, Sep>> Sep <x:T> => util::push(xs, x),
};
